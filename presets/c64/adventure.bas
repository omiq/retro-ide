REM TEXT ADVENTURE GAME BY CHRIS GARRETT 2025 RETROGAMECODERS.COM

REM SET UP VARIABLES ETC
GOSUB INIT

DISPLAYROOM:
REM SHOW ROOM DETAILS
GOSUB CLRSCR
IF PL=0 THEN PL = PP                    : REM PLAYER LOCATION CAN NOT BE 00 AS THAT IS INVENTORY
PP = PL                                 : REM BACKUP THE LOCATION IN CASE ILLEGAL MOVE MADE
PRINT RV$+LO$(PL)+RO$
PRINT ""
IF PL=5 THEN GOSUB YOUWIN
PRINT "OBJECTS VISIBLE:"+LB$
FOR I = 0 TO OC-1                       : REM CHECK OBJECT LOCATIONS FROM THE FIRST OBJECT TO OBJECT COUNT
IF OL(I) = PL THEN PRINT ". ";OB$(I)    : REM IF THE OBJECT IS IN CURRENT LOCATION PRINT IT
NEXT I
PRINT ""
PRINT WT$+"EXITS AVAILABLE:"+LB$
REM CHECK EACH POSSIBLE EXIT
IF MID$(EX$(PL),1,2)<>"00" THEN PRINT ". NORTH"
IF MID$(EX$(PL),3,2)<>"00" THEN PRINT ". EAST"
IF MID$(EX$(PL),5,2)<>"00" THEN PRINT ". SOUTH"
IF MID$(EX$(PL),7,2)<>"00" THEN PRINT ". WEST"
IF MID$(EX$(PL),9,2)<>"00" THEN PRINT ". UP"
IF MID$(EX$(PL),11,2)<>"00" THEN PRINT ". DOWN"


GETCOMMAND:
I$=""
PRINT ""
PRINT YL$+"WHAT NOW?"+LB$
INPUT I$
IF LEFT$(I$,3) = "GO " THEN GOSUB FULLMOVE
IF I$ = "N" THEN GOSUB ABRMOVE
IF I$ = "E" THEN GOSUB ABRMOVE
IF I$ = "S" THEN GOSUB ABRMOVE
IF I$ = "W" THEN GOSUB ABRMOVE
IF I$ = "U" THEN GOSUB ABRMOVE
IF I$ = "D" THEN GOSUB ABRMOVE
IF LEFT$(I$,1) = "I" THEN GOSUB INVENTORY
IF LEFT$(I$,4) = "GET " THEN GOSUB GETOBJECT
IF LEFT$(I$,5) = "TAKE " THEN GOSUB TAKEOBJECT
IF LEFT$(I$,1) = "H" THEN GOSUB HELP
IF LEFT$(I$,4) = "QUIT" THEN GOSUB GAMEOVER
IF LEFT$(I$,4) = "EXIT" THEN GOSUB GAMEOVER
IF LEFT$(I$,5) = "DROP " THEN GOSUB DROPOBJECT
IF LEFT$(I$,8) = "EXAMINE " THEN GOSUB EXAMINEOBJECT
IF LEFT$(I$,4) = "LOOK" OR LEFT$(I$,1) = "L" THEN ?"":PRINT RD$(PL):?"":GOSUB WAITKEY
IF LEFT$(I$,1) = "Q" THEN GOTO GAMEOVER
IF LEFT$(I$,4) = "USE " THEN GOSUB USEOBJECT
GOTO DISPLAYROOM

FULLMOVE:
REM FULLY WRITTEN OUT MOVE (EG. GO SOUTH OR GO S)
D$ = MID$(I$,4,1)
GOSUB MOVES
RETURN

ABRMOVE:
REM ABBREVIATED MOVE (EG. N)
D$ = I$
GOSUB MOVES
RETURN


MOVES:
REM GO TO THE NEW PLAYER LOCATION (PL)
IF D$ = "N" THEN PL = VAL(MID$(EX$(PL),1,2))
IF D$ = "E" THEN PL = VAL(MID$(EX$(PL),3,2))
IF D$ = "S" THEN PL = VAL(MID$(EX$(PL),5,2))
IF D$ = "W" THEN PL = VAL(MID$(EX$(PL),7,2))
IF D$ = "U" THEN PL = VAL(MID$(EX$(PL),9,2))
IF D$ = "D" THEN PL = VAL(MID$(EX$(PL),11,2))

RETURN

INVENTORY:
REM OBJECTS THE PLAYER IS CARRYING
PRINT ""
PRINT "OBJECTS IN YOUR POSSESSION:"
FOR I = 0 TO OC-1                       : REM CHECK OBJECT LOCATION FROM THE FIRST OBJECT TO OBJECT COUNT
IF OL(I) = 0 THEN PRINT ". ";OB$(I)     : REM IF THE OBJECT IS IN ZERO PRINT IT
NEXT I
PRINT ""

WAITKEY:
PRINT CY$+RV$+"        PRESS A KEY TO CONTINUE         "+RO$
WAITINGFORKEY:
GET I$
IF I$="" GOTO WAITINGFORKEY
RETURN

TAKEOBJECT:
REM ALTERNATIVE ACTION TO GET
F=-1:R$=""
R$ = MID$(I$,6)         : REM R$ IS OBJECT REQUESTED
GOTO GETOBJID

GETOBJECT:
REM ALLOW PLAYER TO GET AVAILABLE OBJECT AND PUT IN INVENTORY
F=-1:R$=""
R$ = MID$(I$,5)         : REM R$ IS OBJECT REQUESTED

GETOBJID:
REM GET THE OBJECT ID
FOR I = 1 TO OC 
IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
NEXT I

REM CAN'T FIND IT? 
PRINT ""
IF F=-1 THEN PRINT "CAN'T SEE THAT HERE, CHECK SPELLING AND BE SPECIFIC?" : GOTO DONEGETTING
IF OL(F-1)=PL THEN GOTO GOTIT
IF OL(F-1)=0 THEN PRINT "YOU ALREADY HAVE THAT" : GOTO DONEGETTING
PRINT "I CAN'T SEE THAT AROUND HERE"
GOTO DONEGETTING

GOTIT:
OL(F-1)=0 : REM SET THE OBJECT LOCATION TO INVENTORY AKA ROOM ZERO
PRINT ""
PRINT "GOT THE ";OBJ$(F-1)

DONEGETTING:
PRINT ""
GOSUB WAITKEY
RETURN


DROPOBJECT:
REM DROP OBJECTS THE PLAYER IS CARRYING

F=-1:R$=""
R$ = MID$(I$,6)         : REM R$ IS OBJECT REQUESTED

REM GET THE OBJECT ID
FOR I = 1 TO OC 
IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
NEXT I

REM CAN'T FIND IT? 
PRINT ""
IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO DONEDROPPING
IF OL(F-1)=0 THEN PRINT "OK, DROPPED!" : OL(F-1)=PL : GOTO DONEDROPPING
PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"


DONEDROPPING:
GOSUB WAITKEY
RETURN

EXAMINEOBJECT:
REM EXAMINE OBJECTS THE PLAYER IS CARRYING

F=-1:R$=""
R$ = MID$(I$,9)         : REM R$ IS OBJECT REQUESTED

REM GET THE OBJECT ID
FOR I = 1 TO OC 
IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
NEXT I

REM CAN'T FIND IT? 
PRINT ""
IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO DONEEXAMINING
IF OL(F-1)=0 THEN PRINT OD$(F-1) : GOTO DONEEXAMINING
PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"


DONEEXAMINING:
GOSUB WAITKEY
RETURN

USEOBJECT:
REM USE AN OBJECT THE PLAYER IS CARRYING

F=-1:R$=""
R$ = MID$(I$,5)         : REM R$ IS OBJECT REQUESTED

REM GET THE OBJECT ID
FOR I = 1 TO OC 
IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
NEXT I

REM CAN'T FIND IT? 
PRINT ""
IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO DONEUSING
IF OL(F-1)=0 THEN GOSUB OBJECTACTIONS : GOTO DONEUSING
PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"


DONEUSING:
GOSUB WAITKEY
RETURN


OBJECTACTIONS:
REM ACTIONS FOR USING OBJECTS
IF F=1 AND PL=3 THEN PRINT "BOOM! THE FURNACE EXPLODES, FILLING THE ROOM WITH FIRE AND SMOKE!":GOSUB WAITKEY:GOSUB GAMEOVER: REM MATCHES IN FURNACE ROOM
IF F=1 AND PL=2 THEN PRINT "SUDDENLY THE FURNACE ROARS TO LIFE, FILLING THE ROOM WITH HEAT AND LIGHT!":EX$(2)="010604000300":M=M-1: IF M<=0 THEN PRINT "YOU ARE OUT OF MATCHES":OL(F-1)=-1: RETURN : REM REMOVE MATCHES FROM INVENTORY
IF F=1 AND PL <> 2 THEN PRINT "YOU STRIKE A MATCH AND LIGHT IT, ILLUMINATING THE ROOM FOR A MOMENT.":M=M-1: IF M<=0 THEN PRINT "YOU ARE OUT OF MATCHES":OL(F-1)=-1: REM REMOVE MATCHES FROM INVENTORY
IF F=2 AND PL=4 THEN PRINT "CLICK! THE DOOR HAS UNLOCKED!":EX$(4)="020005000000":OL(F-1)=-1: REM REMOVE KEY FROM INVENTORY
IF F=2 AND PL<>4 THEN PRINT "YOU TRY TO USE THE KEY, BUT IT DOESN'T FIT ANY LOCKS HERE."
RETURN

HELP:
REM SHOW HELP SCREEN                          :                                       :
GOSUB NEWSCREEN
PRINT "YOU HAVE WOKEN UP IN A DARK AND DAMP    BASEMENT, WITH NO MEMORY OF HOW YOU GOT HERE."
PRINT ""
PRINT "YOUR HEAD IS POUNDING AND YOU FEEL      DISORIENTED. YOU NEED TO FIND A WAY OUT."
PRINT ""
PRINT "ENTER THE COMMAND YOU WANT TO USE:"
PRINT "  NORTH, SOUTH, EAST, WEST, UP, DOWN"
PRINT "  (N, S, E, W, U, D)"
PRINT ""
PRINT "  GET <OBJECT>, TAKE <OBJECT>"
PRINT "  DROP <OBJECT>"
PRINT "  EXAMINE <OBJECT>"
PRINT "  L, LOOK"
PRINT "  I, INVENTORY"
PRINT "  H, HELP"
PRINT "  Q, QUIT, EXIT"
PRINT ""
PRINT ""
GOSUB WAITKEY
RETURN

YOUWIN:
REM SHOW WIN SCREEN

GOSUB NEWSCREEN
PRINT "CONGRATULATIONS! YOU HAVE ESCAPED THE   MURDER HOUSE!"
PRINT ""
PRINT "NOW YOU KNOW WHERE THE EXIT IS FEEL FREE TO GO BACK IN AND EXPLORE, JUST DO NOT HANG AROUND TOO LONG!"
PRINT ""
GOSUB WAITKEY
GOSUB CLRSCR
RETURN

NEWSCREEN:
REM CLEAR SCREEN AND SET UP COLOURS
POKE 53281,6 : POKE 53280,6: REM SCREEN COLOURS
POKE 53272, 21 : REM PETSCII
LB$=CHR$(154): WT$=CHR$(5): REM LIGHT BLUE, WHITE
YL$=CHR$(158): CY$=CHR$(159): REM YELLOW AND CYAN
RV$=CHR$(18): RO$=CHR$(146): BL$=CHR$(13) + CHR$(187) + CHR$(32): REM REVERSE ON AND OFF TO MAKE LISTING THE CODE EASIER

CLRSCR:
PRINT WT$: PRINT CHR$(147): PRINT CHR$(19);: REM CLEAR SCREEN, WHITE TEXT
RETURN



INIT: 
REM SET UP THE GAME AND THE DISPLAY
GOSUB NEWSCREEN

REM OBJECTS AND LOCATIONS 
REM =====================
REM OBJECTS
OC = 2 : REM OBJECT COUNT
DIM OB$(OC)
OB$(0)="MATCHES": M=6 : REM MATCHES, 6 IN INVENTORY TO START
OB$(1)="KEY"

REM OBJECT DESCRIPTIONS
DIM OD$(OC)
OD$(0)="A SMALL BOOK OF PROMOTIONAL MATCHES ADVERTISING PATTY'S BAR AND GRILL, NORTH LAKES"
OD$(1)="A LARGE AND HEAVY KEY MADE OUT OF BRASS."

REM LOCATIONS
RC = 6 : REM ROOM COUNT
DIM LO$(RC)
LO$(0)="INVENTORY"
LO$(1)="DANK BASEMENT"
LO$(2)="FURNACE ROOM"
LO$(3)="SERVICE HATCH"
LO$(4)="A STORE ROOM?"
LO$(5)="OUTSIDE THE HOUSE"
LO$(6)="CRAMPED STAIRWELL"


REM ROOM DESCRIPTIONS                          :                                       :                                       :
DIM RD$(RC)
RD$(0)=""
RD$(1)="A CHILLINGLY DAMP, BARE-BRICKED ROOM    WITH POURED CEMENT FLOOR AND TIMBER     BEAMED CEILING. WINDOW FRAMES ARE       BOARDED ALONG ONE WALL."
RD$(2)="THIS ROOM IS OBVIOUSLY A LATER ADDITION, THROWN TOGETHER WITH DRYWALL, AND JUST LARGE ENOUGH TO SECTION OFF THE FURNACE FROM THE MAIN BASEMENT. YOU SMELL GAS."
RD$(3)="UP ABOVE THE FURNACE, THIS TINY SPACE   MUST HAVE BEEN BUILT TO ALLOW ACCESS TO HVAC DUCTING. THE SMELL OF GAS IS STRONG HERE, AND THE FURNACE HUMS OMINOUSLY."
RD$(4)="THE DARK AND DUSTY ROOM IS EMPTY, WITH  A SINGLE LIGHT BULB HANGING FROM THE    CEILING. THERE IS AN OLD WOODEN DOOR ON THE FAR WALL COVERED IN COBWEBS." 
RD$(5)="OUTSIDE THE HOUSE, YOU CAN SEE THE FRONT DOOR AND A PATH LEADING TO THE STREET."
RD$(6)="A TINY, TWISTY STAIRWELL THAT WAS       PREVIOUSLY OBSCURED BY THE DARK."



REM OBJECT'S LOCATIONS
REM LOC 0 = PLAYER'S INVENTORY
DIM OL(OC)
OL(0)=0 : REM MATCHES ARE IN INVENTORY AT THE START OF THE GAME
OL(1)=6 : REM KEY IS IN THE STAIRWELL

REM EXIT NAMES
DIM EN$(6)
EN$(0)="NORTH"
EN$(1)="EAST"
EN$(2)="SOUTH"
EN$(3)="WEST"
EN$(4)="UP"
EN$(5)="DOWN"


REM ROOM EXITS
REM     N E S W U D
DIM EX$(20)
EX$(1)="000002000000"
EX$(2)="010004000300"
EX$(3)="000000000002"
EX$(4)="020000000000"
EX$(5)="040000000000"
EX$(6)="000000020000"


REM PLAYER
REM ======
PL = 1 : REM PLAYER LOCATION
PP = 1 : REM PLAYER PREVIOUS LOCATION


WELCOMESCREEN:
REM WELCOME SCREEN
GOSUB NEWSCREEN
GOSUB INTRO
GOSUB NEWSCREEN

? "             MURDER HOUSE"
? "         A TEXT ADVENTURE GAME"
? "           BY CHRIS GARRETT"
? "                 2025"
?""
? LG$
? "          RETROGAMECODERS.COM"
?""
?" PRESS H FOR HELP OR OTHER KEY TO START"
?""

WAITFORSTARTKEY:
GET I$
IF I$="" THEN GOTO WAITFORSTARTKEY
IF I$="H" THEN GOSUB HELP
IF I$ <> "H" AND I$ <> "" THEN RETURN
RETURN

INTRO:
PRINT CHR$(147)
PRINT "{WHITE}                                  "
PRINT "{WHITE}             {213}{201}         {199}         "
PRINT "{WHITE}             {202}{203}     {RVS}{169}{RVS OFF}{WHITE}{RVS}{WHITE}{223}{RVS OFF}  {221}         "
PRINT "{WHITE}                   {RVS}{169}{RVS OFF}{169}{223}{WHITE}{RVS}{159}{WHITE}{223}{RVS OFF} {WHITE}{199}         "
PRINT "{WHITE}                  {RVS}{169}{RVS OFF}{169}{206}{205}{223}{RVS}{159}{WHITE}{223}{RVS OFF}{144}{WHITE}{161}       "
PRINT "{WHITE}                 {RVS}{169}{RVS OFF}{169}{206}{213}{201}{205}{223}{WHITE}{RVS}{170}{RVS OFF}   {213}{201}    "
PRINT "{WHITE}            {213}{201}   {RVS}{172}{RVS OFF}  {202}{203}  {RVS}{187}{RVS OFF}  {213}{213}{201}    "
PRINT "{WHITE}           {213} {201}{213}{201} {161}{176}{174}  {176}{174}{RVS}{161}{RVS OFF}       "
PRINT "{WHITE}                 {161}{173}{189}  {173}{189}{RVS}{161}{RVS OFF} {168}       "
PRINT "{WHITE}              {168}{220} {161}{176}{174}{207}{208}{176}{174}{RVS}{161}{RVS OFF}{168}{166}{168}      "
PRINT "{WHITE}             {166}{166}{166}{220}{161}{173}{189}{180}{167}{173}{189}{RVS}{161}{RVS OFF}{166}{166}{166}{166}     "
PRINT "{WHITE}            {166}{166}  {168}{168}{168}{168} {184}{168}{168}{168}{168} {166}{166}     "
PRINT "{WHITE}               {168}{168}{168}{168}{168}{184}{184}{168}{168}{168}{168}{168}       "
PRINT "{WHITE}             {168}{168}{168}{168}{168}{168}{168}{184} {168}{168}{168}{168}{168}{168}      "
PRINT "{WHITE}                     {184}            "
PRINT "{WHITE}                                  "
PRINT "{WHITE}         {WHITE}{176}{178}{201}  {213}{201} {221}{213}{201}              "
PRINT "{WHITE}         {PINK}{221}{221}{221}{221}{200}{194} {213}{221}{171}{203}{213}{201} {WHITE}{221}{221}{213}{201}{221}{200}{213}{201}{213}{201} "
PRINT "{WHITE}         {RED}{221} {221}{202}{203}{221} {202}{189}{202}{203}{194}  {PINK}{171}{179}{221}{221}{221}{200}{202}{201}{171}{203} "
PRINT "{WHITE}                       {RED}{221}{221}{202}{203}{202}{203}{202}{203}{202}{203} "

GOSUB WAITKEY
RETURN

GAMEOVER:
GOSUB CLRSCR
POKE 53281,6 : POKE 53280,14
PRINT "GOODBYE!"
PRINT ""
PRINT "MEMORY FREE",FRE(0)
PRINT ""

