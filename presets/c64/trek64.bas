10 REM ** SUPER STAR TREK
15 REM ** BY MIKE MAYFIELD 1974
20 REM ** MODIFIED BY DAVID AHL 1978
25 REM ** CONVERTED TO 40 COLUMNS
30 REM ** BY ELECTRON.GREG 2022-2023
35 REM ** MODIFIED BY CHRIS GARRETT 2025
40 REM ** ( FOR TUTORIAL USE )
45 REM ** SUPPORT ELECTRON.GREG 
50 REM ** https://electrongreg.itch.io/
55 REM **
85 CLR : Z2$="" : ATAKFLAG=0 : SLSFLAG=0
90 REM ** COLOURS AT END OF LISTING **
95 GOSUB 6500
100 PRINT "{CLR}"
110 PRINT HCOL$;"   --S-U-P-E-R---S-T-A-R---T-R-E-K--"
120 FOR N=1 TO 5 : PRINT : NEXT : PRINT BCOL$;
130 PRINT"                    ,------*------,"
140 PRINT"    ,-------------   ---  ------"
150 PRINT"     -------- --      / /"
160 PRINT"         ,--- -------/ /--,"
170 PRINT"          ----------------" : PRINT DCOL$
180 PRINT"    THE USS ENTERPRISE --- NCC-1701"
190 FOR N=1 TO 5 : PRINT FCOL$ : NEXT : CRSTART=1 : GOSUB 6020
200 X9=RND(-TI) : REM ** RANDOM SEED GENERATOR **
210 PRINT : PRINT "           GENERATING GALAXY";
220 Z$="                         "
230 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
240 T=INT(RND(1)*20+20)*100 : T0=T : T9=25+INT(RND(1)*10) : D0=0 : E=3000 : E0=E
250 P=10 : P0=P : S9=200 : S=0 : B9=2 : K9=0 : X$="" : X0$=" IS "
260 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
270 DEF FNR(R)=INT(RND(R)*7.98+1.01)
280 REM INITIALIZE ENTERPRISES POSITION
290 Q1=FNR(1) : Q2=FNR(1) : S1=FNR(1) : S2=FNR(1)
300 FORI=1TO9 : C(I,1)=0 : C(I,2)=0 : NEXTI
310 C(3,1)=-1 : C(2,1)=-1 : C(4,1)=-1 : C(4,2)=-1 : C(5,2)=-1 : C(6,2)=-1
320 C(1,2)=1 : C(2,2)=1 : C(6,1)=1 : C(7,1)=1 : C(8,1)=1 : C(8,2)=1 : C(9,2)=1
330 FORI=1TO8 : D(I)=0 : NEXTI
340 A1$="NAVSRSLRSPHATORSHEDAMCOMXXXHLP"
350 REM SETUP WHAT EXISTS IN GALAXY . . .
360 REM K3= # KLINGONS  B3= # STARBASES  S3 = # STARS
370 FORI=1TO8 : PRINT "."; : FORJ=1TO8 : K3=0 : Z(I,J)=0 : R1=RND(1)
380 IFR1>.98THENK3=3 : K9=K9+3 : GOTO410
390 IFR1>.95THENK3=2 : K9=K9+2 : GOTO410
400 IFR1>.80THENK3=1 : K9=K9+1
410 B3=0 : IFRND(1)>.96THENB3=1 : B9=B9+1
420 G(I,J)=K3*100+B3*10+FNR(1) : NEXTJ : NEXTI : IFK9>T9THENT9=K9+1
430 PRINT"{CLR}"
440 GOSUB 6080 : REM ** KEY TO SRS ICONS **
450 REM GOSUB 6150 : REM ** USE THESE COMMANDS LIST **
460 PRINT : GOSUB 6020 : REM ** PAUSE **
470 IFB9<>0THEN GOTO 500
480 IFG(Q1,Q2)<200THENG(Q1,Q2)=G(Q1,Q2)+120 : K9=K9+1
490 B9=1 : G(Q1,Q2)=G(Q1,Q2)+10 : Q1=FNR(1) : Q2=FNR(1)
500 K7=K9 : IFB9<>1THENX$="S" : X0$=" ARE "
510 PRINT "{CLR}" : PRINT"YOUR ORDERS ARE AS FOLLOWS:" : PRINT
520 PRINT"DESTROY THE";K9;"KLINGON WARSHIPS BEFORE"
530 PRINT"STARDATE";T0+T9;". THIS GIVES YOU";T9;"DAYS."
540 PRINT "THERE";X0$;B9;"STARBASE";X$;" IN THE GALAXY"
550 PRINT"FOR RESUPPLYING & REPAIRING YOUR SHIP."
560 I=RND(1) 
570 REM HERE ANY TIME NEW QUADRANT ENTERED
580 Z4=Q1 : Z5=Q2 : K3=0 : B3=0 : S3=0 : G5=0 : D4=.5*RND(1) : Z(Q1,Q2)=G(Q1,Q2)
590 IFQ1<1ORQ1>8ORQ2<1ORQ2>8THEN GOTO 700
600 GOSUB 5610 : PRINT : IF T0<>T THEN GOTO 650
610 PRINT"YOUR MISSION BEGINS WITH YOUR STARSHIP"
620 PRINT"IN THE QUADRANT, ";G2$;"."
630 SRSFLAG=1
635 PRINT : GOSUB 6020 : REM ** PAUSE **
640 PRINT "{CLR}" : GOTO 670
650 IF ATAKFLAG=1 THEN GOSUB 6020 : ATAKFLAG=0 : PRINT
660 SRSFLAG=1 : PRINT"ENTERING ";G2$;" QUADRANT..."
670 K3=INT(G(Q1,Q2)*.01) : B3=INT(G(Q1,Q2)*.1)-10*K3
680 S3=G(Q1,Q2)-100*K3-10*B3
690 FORI=1TO3 : K(I,1)=0 : K(I,2)=0 : NEXTI
700 FORI=1TO3 : K(I,3)=0 : NEXTI : Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
710 REM POSITION ENTERPRISE IN QUADRANT, THEN PLACE "K3" KLINGONS, &
720 REM "B3" STARBASES, & "S3" STARS ELSEWHERE.
730 A$=BCOL$+"E"+FCOL$ : Z1=S1 : Z2=S2 : GOSUB 5400 : IFK3<1THEN GOTO 760
740 FORI=1TOK3 : GOSUB 5370 : A$=ECOL$+"K"+FCOL$ : Z1=R1 : Z2=R2
750 GOSUB 5400 : K(I,1)=R1 : K(I,2)=R2 : K(I,3)=S9*(0.5+RND(1)) : NEXTI
760 IFB3<1THEN GOTO 780
770 GOSUB 5370 : A$=DCOL$+"B"+FCOL$ : Z1=R1 : B4=R1 : Z2=R2 : B5=R2 : GOSUB 5400
780 FORI=1TOS3 : GOSUB 5370 : A$=HCOL$+"*"+FCOL$ : Z1=R1 : Z2=R2 : GOSUB 5400 : NEXTI
790 GOSUB 3580
800 IFS+E>10THENIFE>10ORD(7)=0THEN GOTO 890
810 PRINT : PRINT"** FATAL ERROR **"
820 PRINT"YOUVE STRANDED YOUR SHIP IN SPACE."
830 PRINT"YOU HAVE INSUFFICIENT MANOEUVRING"
840 PRINT"ENERGY, & SHIELD CONTROL IS PRESENTLY"
850 PRINT"INCAPABLE OF CROSS-CIRCUITING TO"
860 PRINT"ENGINE ROOM!!"
870 PRINT : GOSUB 6020
880 GOTO 3370
890 PRINT : SRSFLAG=0 : LX=3 : PRINT"COMMAND :  "; : GOSUB 5860
900 A$=LII$ : Z2$=A$ : ATAKFLAG=0
905 IF A$="SLS" THEN SLSFLAG=1
906 IF A$="SLS" THEN PRINT : PRINT "SHORT & LONG RANGE SCAN..." : GOTO 3570
910 IF A$<>"KEY" THEN GOTO 940
920 GOSUB 6080 : REM KEY TO SRS ICONS
930 GOTO 890
940 COMFLAG=0 : IF A$="GAL" AND D(8)>=0 THEN A$="COM" : COMFLAG=1
945 IF D(8)<0 AND A$="GAL" THEN PRINT : PRINT"SHIPS COMPUTER DISABLED" : GOTO 890
950 FORI=1TO10 : IFLEFT$(A$,3)<>MID$(A1$,3*I-2,3)THEN GOTO 970
960 ON I GOTO 1000,790,1850,2110,2380,2820,2950,4140,3420,6150
970 NEXTI : GOSUB 6150
980 GOTO 800
990 REM COURSE CONTROL BEGINS HERE
1000 GOSUB 6290 : REM ** DIRECTION HELPER **
1010 PRINT : LX=5 : PRINT"COURSE (1-9) :  "; : GOSUB 5860
1020 C1=VAL(LII$) : IFC1=9THENC1=1
1030 IFC1>=1ANDC1<9THEN GOTO 1060
1040 PRINT : PRINT"LT. SULU REPORTS, INCORRECT COURSE"
1050 PRINT"DATA, SIR!" : GOTO800
1060 X$="8" : IFD(1)<0THENX$="0.2"
1070 SRSFLAG=1 : LX=5 : PRINT"WARP FACTOR (0-";X$;") :  "; : GOSUB 5860
1080 W1=VAL(LII$) : IFD(1)<0ANDW1>.2THEN GOTO 1130
1090 IF W1>0 AND W1<=8 THEN GOTO 1150
1100 IF W1=0 THEN GOTO 800
1110 PRINT : PRINT"CHIEF ENGINEER SCOTT REPORTS THE"
1120 PRINT"ENGINES WONT TAKE WARP ";W1;"!" : GOTO 800
1130 PRINT : PRINT"WARP ENGINES ARE DAMAGED."
1140 PRINT"MAXIMUM SPEED = WARP 0.2" :  : GOTO 800
1150 N=INT(W1*8+.5) : IFE-N>=0THEN GOTO 1240
1160 PRINT : PRINT"ENGINEERING REPORTS INSUFFICIENT ENERGY";
1170 PRINT"AVAILABLE FOR WARP";W1;"!"
1180 IFS<N-EORD(7)<0THEN GOTO 800
1190 PRINT : PRINT"DEFLECTOR CONTROL ROOM ACKNOWLEDGES"
1200 S1$=STR$(S)
1210 PRINT "SHIELD ENERGY DEPLOYED IS";S1$;" UNITS."
1220 GOTO 800
1230 REM KLINGONS MOVE/FIRE ON MOVING STARSHIP . . .
1240 FORI=1TOK3 : IFK(I,3)=0THEN GOTO 1270
1250 A$="   " : Z1=K(I,1) : Z2=K(I,2) : GOSUB 5400 : GOSUB 5370
1260 K(I,1)=Z1 : K(I,2)=Z2 : A$=ECOL$+"K"+FCOL$ : GOSUB 5400
1270 NEXTI : GOSUB 3210 : D1=0 : D6=W1 : IFW1>=1THEND6=1
1280 FORI=1TO8 : IFD(I)>=0THEN GOTO 1330
1290 D(I)=D(I)+D6 : IFD(I)>-.1ANDD(I)<0THEND(I)=-.1 : GOTO1330
1300 IFD(I)<0THEN GOTO 1330
1310 IFD1<>1THEND1=1 : PRINT : PRINT"DAMAGE CONTROL REPORT :   "
1320 R1=I : GOSUB 5460 : PRINTG2$;" REPAIR COMPLETED"
1330 NEXTI
1340 IFRND(1)>.2THEN GOTO 1410
1350 R1=FNR(1) : IFRND(1)>=.6THEN GOTO 1380
1360 D(R1)=D(R1)-(RND(1)*5+1) : PRINT : PRINT"DAMAGE CONTROL REPORTS:"
1370 GOSUB 5460 : PRINTG2$;" DAMAGED" : GOTO 1410
1380 D(R1)=D(R1)+RND(1)*3+1 : PRINT : PRINT"DAMAGE CONTROL REPORTS:"
1390 GOSUB 5460 : PRINTG2$;" PARTLY REPAIRED"
1400 REM BEGIN MOVING STARSHIP
1410 A$="   " : Z1=INT(S1) : Z2=INT(S2) : GOSUB 5400
1420 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)) : X=S1 : Y=S2
1430 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)) : Q4=Q1 : Q5=Q2
1440 FORI=1TON : S1=S1+X1 : S2=S2+X2 : IFS1<1ORS1>=9ORS2<1ORS2>=9THEN GOTO 1580
1450 S8=INT(S1)*24+INT(S2)*3-26 : IFMID$(Q$,S8,2)="  "THEN GOTO 1510
1460 S1=INT(S1-X1) : S2=INT(S2-X2) : PRINT : PRINT"WARP ENGINES SHUT DOWN AT ";
1470 PRINT"SECTOR";S1;",";S2
1480 PRINT"DUE TO BAD NAVIGATION"
1490 SRSFLAG=1
1500 GOTO 1520
1510 NEXTI : S1=INT(S1) : S2=INT(S2)
1520 A$=BCOL$+"E"+FCOL$ : Z1=INT(S1) : Z2=INT(S2) : GOSUB 5400 : GOSUB 1790 : T8=1
1530 IFW1<1THENT8=.1*INT(10*W1)
1540 T=T+T8 : IFT>T0+T9THEN GOTO 3370
1550 REM SEE IF DOCKED, THEN GET COMMAND
1560 GOTO 790
1570 REM EXCEEDED QUADRANT LIMITS
1580 X=8*Q1+X+N*X1 : Y=8*Q2+Y+N*X2 : Q1=INT(X/8) : Q2=INT(Y/8) : S1=INT(X-Q1*8)
1590 S2=INT(Y-Q2*8) : IFS1=0THENQ1=Q1-1 : S1=8
1600 IFS2=0THENQ2=Q2-1 : S2=8
1610 X5=0 : IFQ1<1THENX5=1 : Q1=1 : S1=1
1620 IFQ1>8THENX5=1 : Q1=8 : S1=8
1630 IFQ2<1THENX5=1 : Q2=1 : S2=1
1640 IFQ2>8THENX5=1 : Q2=8 : S2=8
1650 IFX5=0THEN GOTO 1760
1660 PRINT : PRINT"LT. UHURA REPORTS MESSAGE FROM STARFLEET";
1670 PRINT"COMMAND :  PERMISSION TO ATTEMPT CROSSING";
1680 PRINT"OF GALACTIC PERIMETER IS HEREBY";
1690 PRINT"*DENIED*. SHUT DOWN YOUR ENGINES.";
1700 PRINT : PRINT"CHIEF ENGINEER SCOTT REPORTS WARP";
1710 PRINT"ENGINES SHUT DOWN AT SECTOR";S1;",";S2;
1720 PRINT"OF QUADRANT";Q1;",";Q2;"."
1730 SRSFLAG=1
1740 PRINT : GOSUB 6020
1750 IFT>T0+T9THEN GOTO 3370
1760 IF8*Q1+Q2=8*Q4+Q5THEN GOTO 1520
1770 T=T+1 : GOSUB 1790 : GOTO 580
1780 REM MANEUVER ENERGY S/R **
1790 E=E-N-10 : IFE>=0THENRETURN
1800 PRINT : PRINT"SHIELD CONTROL SUPPLIES ENERGY TO"
1810 PRINT"COMPLETE THE MANOEUVRE."
1820 S=S+E : E=0 : IFS<=0THENS=0
1830 RETURN
1840 REM LONG RANGE SCAN CODE
1850 K1=0
1860 IFD(3)<0THENPRINT : PRINT"LONG RANGE SENSORS ARE INOPERABLE"
1865 IFD(3)<0THEN SLSFLAG=0 : GOTO800
1870 PRINT : PRINT"LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
1880 PRINT : O1$=" +-----+-----+-----+" : PRINTO1$;
1890 PRINT "         ";ECOL$;"#";DCOL$;"#";HCOL$;"#";FCOL$
1900 FORI=Q1-1TOQ1+1 : N(1)=-1 : N(2)=-2 : N(3)=-3 : FORJ=Q2-1TOQ2+1
1910 IFI>0ANDI<9ANDJ>0ANDJ<9THENN(J-Q2+2)=G(I,J) : Z(I,J)=G(I,J)
1920 NEXTJ : FORL=1TO3
1930 IF K1=2 AND L=3 THEN PRINT CHR$(146);"";
1940 PRINT" :"; : REM BORDER 
1950 IF K1=2 AND L=2 THEN PRINT " ";CHR$(18); : GOTO 1970
1960 PRINT " "; 
1970 IFN(L)<0THEN PRINT"***"; : GOTO 2000
1980 G1$=RIGHT$(STR$(N(L)+1000),3)
1982 G2$=ECOL$+MID$(G1$,1,1) : PRINT G2$;
1984 G2$=DCOL$+MID$(G1$,2,1) : PRINT G2$;
1990 G2$=HCOL$+MID$(G1$,3,1) : PRINT G2$;FCOL$;
2000 NEXTL : PRINT" :"; : K1=K1+1
2010 IF K1=1 THEN PRINT "        . . ."
2020 IF K1=3 THEN PRINT "      .   .   ."
2030 IF K1=5 THEN PRINT "    .           ."
2040 K1=K1+1 : PRINTO1$;
2050 IF K1=2 THEN PRINT "       .  .  ."
2060 IF K1=4 THEN PRINT "     .  BASES";
2070 IF K1=4 THEN PRINT "  ."
2080 IF K1=6 THEN PRINT "  KLINGONS    STARS"
2090 NEXTI : K1=0 : IF SLSFLAG=1 THEN SLSFLAG=0
2095 GOTO 800
2100 REM PHASER CONTROL CODE BEGINS HERE
2110 IFD(4)<0THEN PRINT : PRINT"PHASERS INOPERATIVE" : GOTO 800
2120 IFK3>0THEN GOTO 2150
2130 PRINT : PRINT"SCIENCE OFFICER SPOCK REPORTS SENSORS"
2140 PRINT"SHOW NO ENEMY SHIPS IN THIS QUADRANT" : GOTO 800
2150 IFD(8)<0THENPRINT : PRINT"COMPUTER FAILURE HAMPERS ACCURACY"
2160 PRINT : PRINT"PHASERS LOCKED ON TARGET!  "
2170 PRINT : PRINT"ENERGY AVAILABLE =";E;"UNITS"
2180 LX=5 : PRINT "NUMBER OF UNITS TO FIRE :  "; : GOSUB 5860
2190 X=VAL(LII$) : IFX<=0THEN GOTO 800
2200 IFE-X<0THEN GOTO 2170
2210 E=E-X : IFD(7)<0THENX=X*RND(1)
2220 H1=INT(X/K3) : FORI=1TO3 : IFK(I,3)<=0THEN GOTO 2340
2230 H=INT((H1/FND(0))*(RND(1)+2)) : IFH>.15*K(I,3)THEN GOTO 2270
2240 PRINT : PRINT" SENSORS SHOW NO DAMAGE TO ENEMY"
2250 PRINT" AT ";K(I,1);",";K(I,2)
2260 GOTO 2340
2270 K(I,3)=K(I,3)-H : PRINT
2280 PRINT H;"UNIT HIT KLINGON AT";K(I,1);",";K(I,2)
2290 IFK(I,3)<=0THENPRINT" *** KLINGON DESTROYED ***" : GOTO 2320
2300 PRINT" (SENSORS SHOW";INT(K(I,3));"UNITS REMAINING)"
2310 GOTO 2340
2320 K3=K3-1 : K9=K9-1 : Z1=K(I,1) : Z2=K(I,2) : A$="   " : GOSUB 5400
2330 K(I,3)=0 : G(Q1,Q2)=G(Q1,Q2)-100 : Z(Q1,Q2)=G(Q1,Q2) : IFK9<=0THEN GOTO 3510
2340 NEXTI
2350 IF K3>0 THEN PRINT : GOSUB 6020
2360 GOSUB 3210 : GOTO 800
2370 REM PHOTON TORPEDO CODE BEGINS HERE
2380 IFP<=0THEN PRINT : PRINT"ALL PHOTON TORPEDOES EXPENDED" : GOTO 800
2390 IFD(5)<0THEN PRINT : PRINT"PHOTON TUBES ARE NOT OPERATIONAL" : GOTO 800
2400 GOSUB 6290 : REM ** DIRECTION HELPER **
2410 PRINT : LX=5 : PRINT "PHOTON TORPEDO COURSE (1-9) :  "; : GOSUB 5860
2420 C1=VAL(LII$) : IFC1=9THENC1=1
2430 IFC1>=1ANDC1<9THEN GOTO 2470
2440 PRINT : PRINT"ENSIGN CHEKOV REPORTS, INCORRECT"
2450 PRINT"COURSE DATA, SIR!"
2460 GOTO 800
2470 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)) : E=E-2 : P=P-1
2480 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)) : X=S1 : Y=S2
2490 PRINT : PRINT"TORPEDO TRACKING:"
2500 X=X+X1 : Y=Y+X2 : X3=INT(X+.5) : Y3=INT(Y+.5)
2510 IFX3<1ORX3>8ORY3<1ORY3>8THEN GOTO 2780
2520 PRINT" ";X3;",";Y3 : A$="   " : Z1=X : Z2=Y : GOSUB 5560
2530 IFZ3<>0THEN GOTO 2500
2540 A$=ECOL$+"K"+FCOL$ : Z1=X : Z2=Y : GOSUB 5560 : IFZ3=0THEN GOTO 2610
2550 PRINT" *** KLINGON DESTROYED ***"
2560 K3=K3-1 : IF K3>0 THEN PRINT : GOSUB 6020
2570 K9=K9-1 : IFK9<=0THEN GOTO 3510
2580 FORI=1TO3 : IFX3=K(I,1)ANDY3=K(I,2)THEN GOTO 2600
2590 NEXTI : I=3
2600 K(I,3)=0 : GOTO 2760
2610 A$=HCOL$+"*"+FCOL$ : Z1=X : Z2=Y : GOSUB 5560 : IFZ3=0THEN GOTO 2650
2620 PRINT" ** STAR AT";X3;",";Y3;"ABSORBED TORPEDO **"
2630 IF K3<>0 THEN PRINT : GOSUB 6020
2640 GOSUB 3210 : GOTO 800
2650 A$=DCOL$+"B"+FCOL$ : Z1=X : Z2=Y : GOSUB 5560 : IFZ3=0THEN GOTO 2400
2660 PRINT" *** STARBASE DESTROYED ***"
2670 PRINT : GOSUB 6020
2680 B3=B3-1 : B9=B9-1
2690 IFB9>0ORK9>T-T0-T9THEN GOTO 2740
2700 PRINT : PRINT"THAT DOES IT, CAPTAIN!!  YOU ARE HEREBY"
2710 PRINT"RELIEVED OF COMMAND AND SENTENCED TO 99"
2720 PRINT"STARDATES AT HARD LABOUR ON CYGNUS 12!!"
2730 GOTO 3420
2740 PRINT : PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD"
2750 PRINT"TO CONSIDER COURT MARTIAL!" : D0=0
2760 Z1=X : Z2=Y : A$="   " : GOSUB 5400
2770 G(Q1,Q2)=K3*100+B3*10+S3 : Z(Q1,Q2)=G(Q1,Q2) : GOSUB 3210 : GOTO 800
2780 PRINT" ** TORPEDO MISSED **"
2790 IF K3<>0 THEN PRINT : GOSUB 6020
2800 GOSUB 3210 : GOTO 800
2810 REM SHIELD CONTROL
2820 IFD(7)<0THEN PRINT : PRINT"SHIELD CONTROL INOPERABLE" : GOTO800
2830 PRINT : PRINT"ENERGY AVAILABLE =";E+S
2840 LX=5 : PRINT"NUMBER OF UNITS TO SHIELDS :  "; : GOSUB 5860
2850 X=VAL(LII$)
2860 IFX<0ORS=XTHEN PRINT"<SHIELDS UNCHANGED>" : GOTO 800
2870 IFX<=E+STHEN GOTO 2910
2880 PRINT : PRINT"SHIELD CONTROL REPORTS THIS IS NOT THE"
2890 PRINT"FEDERATION TREASURY."
2900 PRINT"<SHIELDS UNCHANGED>" : GOTO 800
2910 E=E+S-X : S=X : PRINT : PRINT"DEFLECTOR CONTROL ROOM REPORT:"
2920 PRINT"SHIELDS NOW AT";INT(S);"UNITS PER"
2930 PRINT"YOUR COMMAND." : GOTO 800
2940 REM DAMAGE CONTROL
2950 IFD(6)>=0THEN GOTO 3100
2960 PRINT : PRINT"DAMAGE CONTROL REPORT NOT AVAILABLE"
2970 IFD0=0THEN GOTO 800
2980 D3=0 : FORI=1TO8 : IFD(I)<0THEND3=D3+.1
2990 NEXTI : IFD3=0THEN GOTO 800
3000 D3=D3+D4 : IFD3>=1THEND3=.9
3010 PRINT : PRINT"TECHNICIANS STANDING BY TO EFFECT"
3020 PRINT"REPAIRS TO YOUR SHIP;"
3030 PRINT"ESTIMATED TIME TO REPAIR:"
3040 PRINT .01*INT(100*D3);"STARDATES"
3050 LX=1 : PRINT : PRINT "AUTHORISE THE REPAIR ORDER (Y/N)? "; : GOSUB 5860
3060 A$=LII$
3070 IF A$<>"Y" THEN GOTO 800
3080 FORI=1TO8 : IFD(I)<0THEND(I)=0
3090 NEXTI : T=T+D3+.1
3100 PRINT : PRINT" SYSTEM              STATE OF REPAIR"
3110 PRINT      " ------------------- -----------------"
3120 FORR1=1TO8
3130 GOSUB 5460 : PRINT" ";G2$;LEFT$(Z$,20-LEN(G2$));
3140 D2=INT(D(R1)*100)*.01
3150 IF D2<0 THEN PRINT  CCOL$;"DAMAGED     ";D2;FCOL$
3160 IF D2>0 THEN PRINT  "OPERATIONAL ";D2
3170 IF D2=0 THEN PRINT  "OPERATIONAL ";D2
3180 NEXTR1 : IFD0<>0THEN GOTO 2980
3190 GOTO 800
3200 REM KLINGONS SHOOTING
3210 IFK3<=0THENRETURN
3220 IFD0<>0THENPRINT : PRINT"STARBASE SHIELDS PROTECT THE ENTERPRISE"
3230 IF D0<>0 THEN RETURN
3240 FORI=1TO3 : IFK(I,3)<=0THEN GOTO 3330
3250 H=INT((K(I,3)/FND(1))*(2+RND(1))) : S=S-H : K(I,3)=K(I,3)/(3+RND(0))
3260 PRINT : PRINTH;"UNIT HIT ENTERPRISE FROM";K(I,1);",";K(I,2)
3270 IFS<=0THEN GOTO 3380
3280 PRINT" <SHIELDS DOWN TO";S;"UNITS>" : IFH<20THEN GOTO 3330
3290 IFRND(1)>.6ORH/S<=.02THEN GOTO 3330
3300 R1=FNR(1) : D(R1)=D(R1)-H/S-.5*RND(1) : GOSUB 5460
3310 PRINT : PRINT"DAMAGE CONTROL REPORTS :  "
3320 PRINT G2$;" DAMAGED BY THE HIT"
3330 NEXTI
3340 ATAKFLAG=1
3350 RETURN
3360 REM END OF GAME
3370 PRINT : PRINT"IT IS STARDATE";T : GOTO 3420
3380 PRINT : GOSUB 6020
3390 PRINT : PRINT"THE ENTERPRISE HAS BEEN DESTROYED. THE"
3400 PRINT"FEDERATION WILL BE CONQUERED"
3410 GOTO 3370
3420 PRINT : PRINT"THERE WERE";K9;"KLINGON BATTLE CRUISERS"
3430 PRINT"LEFT AT THE END OF YOUR MISSION."
3440 IFB9=0THEN GOTO 3500
3450 PRINT : PRINT"THE FEDERATION IS IN NEED OF A NEW"
3460 PRINT"STARSHIP COMMANDER FOR ANOTHER MISSION."
3470 PRINT : PRINT"... IF THERE IS A VOLUNTEER,"
3480 LX=3 : PRINT "STEP FORWARD AND ENTER AYE :  "; : GOSUB 5860
3490 A$=LII$ : IFA$="AYE"THEN GOTO 10
3500 END
3510 PRINT : GOSUB 6020
3520 PRINT : PRINT"CONGRATULATIONS, CAPTAIN! THE LAST"
3530 PRINT"KLINGON BATTLE CRUISER MENACING THE"
3540 PRINT"FEDERATION HAS BEEN DESTROYED."
3550 PRINT : PRINT"YOUR EFFICIENCY RATING IS";
3560 PRINT INT(1000*(K7/(T-T0))^2) : GOTO 3440
3570 REM SHORT RANGE SCAN & STARTUP SUBROUTINE
3580 IF ATAKFLAG=1 THEN PRINT : GOSUB 6020
3590 IFD(2)<0 THEN GOTO 3720
3600 REM
3610 IF SRSFLAG=0 THEN PRINT : PRINT"{CLR}SHORT RANGE SCAN + SUMMARY DATA"
3620 SRSFLAG=0 : ATAKFLAG=0
3630 FORI=S1-1TOS1+1 : FORJ=S2-1TOS2+1
3640 IFINT(I+.5)<1ORINT(I+.5)>8ORINT(J+.5)<1ORINT(J+.5)>8THEN GOTO 3660
3650 A$=DCOL$+"B"+FCOL$ : Z1=I : Z2=J : GOSUB 5560 : IFZ3=1THEN GOTO 3670
3660 NEXTJ : NEXTI : D0=0 : GOTO 3690
3670 D0=1 : C$=DCOL$+"DOCKED"+FCOL$ : E=E0 : P=P0
3680 PRINT : PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES" : S=0 : GOTO 3710
3690 IFK3>0THENC$=CCOL$+CHR$(18)+"*RED*"+CHR$(146)+FCOL$ : GOTO 3710
3700 C$=FCOL$+"GREEN"+FCOL$ : IFE<E0*.1THENC$=ICOL$+"AMBER"+FCOL$
3710 IFD(2)>=0THEN GOTO 3730
3720 PRINT : PRINT"*** SHORT RANGE SENSORS ARE OUT ***"
3722 IF SLSFLAG=1 THEN GOTO 1840
3725 RETURN
3730 IF K3=0 THEN GOTO 3770
3740 PRINT : PRINT CCOL$;"COMBAT AREA  ";
3750 PRINT"** CONDITION RED **";FCOL$
3770 LOW$=CCOL$+CHR$(18)+"LOW!"+CHR$(146)+FCOL$ : PRINT
3790 PRINT "   1 2 3 4 5 6 7 8"
3810 PRINT "  +-+-+-+-+-+-+-+-+ STARDATE  ";INT(T*10)*.1
3820 FOR I=1 TO 8
3830 I$=STR$(I)
3840 PRINT I$;":"; : REM BORDER
3850 J1=(I-1)*24+1 : J2=(I-1)*24+22
3860 FORJ=J1 TO J2 STEP3
3870 Z3$=MID$(Q$,J,3)
3880 IF Z3$="   " THEN PRINT " "; : GOTO 3930
3920 PRINT Z3$;
3930 IF J=J2 THEN PRINT ":";
3940 IF J<>J2 THEN PRINT " ";
3950 NEXTJ
3960 IF I=1 THEN PRINT" DAYS LEFT ";.1*INT((T0+T9-T)*10);
3970 IF I=2 THEN PRINT" CONDITION  ";
3980 IF I=2 THEN PRINT C$;
3990 IF I=3 THEN PRINT" QUADRANT  ";Q1;",";Q2;
4000 IF I=4 THEN PRINT" SECTOR    ";S1;",";S2;
4010 IF I=5 THEN PRINT" TORPEDOES ";INT(P);
4020 IF I=6 THEN PRINT" ENERGY    ";INT(E+S);
4030 IF I=7 THEN PRINT" SHIELDS   ";INT(S);
4040 IF I=7 AND S<201 AND K3>0 THEN PRINT LOW$;
4050 IF I=8 THEN         PRINT" KLINGONS  ";INT(K9);
4060 PRINT
4070 NEXT I : PRINT "  +-+-+-+-+-+-+-+-+";
4080 MW=E/8 : MW=MW*10 : MW=INT(MW) : MW=MW/10
4090 IF MW>8 THEN MW=8
4100 IF D(1)<0 AND MW>0.2 THEN MW=0.2
4110 PRINT" MAX WARP  ";MW
4115 IF SLSFLAG=1 THEN GOTO 1840
4120 RETURN
4130 REM LIBRARY COMPUTER CODE
4140 IFD(8)<0THEN PRINT : PRINT"SHIPS COMPUTER DISABLED" :  GOTO800
4150 PRINT : PRINT"FUNCTIONS AVAILABLE FROM COMPUTER:" : PRINT
4160 PRINT" 0 - CUMULATIVE GALACTIC RECORD"
4170 PRINT" 1 - STATUS & DAMAGE REPORT"
4180 PRINT" 2 - PHOTON TORPEDO TARGETING DATA"
4190 PRINT" 3 - STARBASE NAV DATA"
4200 PRINT" 4 - DIRECTION/DISTANCE CALCULATOR"
4210 PRINT" 5 - GALAXY REGION NAME MAP"
4220 PRINT : LX=1 : PRINT"COMPUTER ACTIVE & AWAITING COMMAND :  ";
4230 IF COMFLAG=1 THEN PRINT "0" : A=0 : GOTO 4270
4240 GOSUB 5860
4250 A=VAL(LII$) : A1=A : IFA<0 OR A>5THEN GOTO 890
4260 IF LII$="" THEN GOTO 890
4270 H8=1 : ON A+1 GOTO 4310,4670,4810,5330,4880,4290
4280 REM SETUP TO CHANGE CUM GAL RECORD TO GALAXY MAP
4290 H8=0 : G5=1 : PRINT : PRINT"THE GALAXY:" : PRINT : PRINT"   "; : GOTO 4340
4300 REM CUM GALACTIC RECORD
4310 PRINT : GALFLAG=0
4320 PRINT"COMPUTER RECORD OF GALAXY FOR" : PRINT"QUADRANT";Q1;",";Q2
4330 PRINT : PRINT"   ";
4340 FOR J=1 TO 8 : J$=STR$(J) : J$=RIGHT$(J$,1)
4350 IF A1=5 THEN GOSUB 6390 : GOTO 4380
4360 IF J=Q2 THEN PRINT " ";CHR$(18);J$;CHR$(146);"  ";
4370 IF J<>Q2 THEN PRINT CHR$(146);J;" ";
4380 NEXT J
4390 PRINT
4400 O1$="  +---+---+---+---+---+---+---+---+"
4410 PRINTO1$ : FORI=1TO8 : I$=STR$(I) : I$=RIGHT$(I$,1)
4420 IF I=Q1 THEN PRINT" ";CHR$(18);I$;CHR$(146);"";
4430 IF A1=5 AND I=Q1 THEN GOTO 4540
4440 IF I<>Q1 THEN PRINT" ";I$; : IFH8=0THEN GOTO 4540
4450 FORJ=1TO8
4460 IF I=Q1 AND J=Q2 THEN PRINT":";CHR$(18); : GALFLAG=1 : GOTO 4490
4470 IF I=Q1 AND J-1=Q2 THEN GOTO 4490
4480 IF I<>Q1 OR J<>Q2 THEN PRINT":";
4490 IFZ(I,J)=0THEN PRINT"   "; : GOTO 4530
4500 G1$=RIGHT$(STR$(Z(I,J)+1000),3)
4502 G2$=ECOL$+MID$(G1$,1,1) : PRINT G2$;
4504 G2$=DCOL$+MID$(G1$,2,1) : PRINT G2$;
4506 G2$=HCOL$+MID$(G1$,3,1) : PRINT G2$;FCOL$;
4510 REM PRINT G2$;
4520 IF GALFLAG=1 THEN PRINT CHR$(146);":"; : GALFLAG=0
4530 NEXTJ : GOTO 4620
4540 Z4=I : Z5=1 : GOSUB 5610 : J0=INT(11-.5*LEN(G2$))
4550 PRINT":";
4560 IF I=Q1 AND Q2<5 THEN PRINT CHR$(18);
4570 PRINTTAB(J0);G2$;CHR$(146);
4580 PRINTTAB(18);":";
4590 Z5=5 : GOSUB 5610 : J0=INT(27-.5*LEN(G2$))
4600 IF I=Q1 AND Q2>4 THEN PRINT CHR$(18);
4610 PRINTTAB(J0);G2$;CHR$(146); : PRINTTAB(34);":";
4620 IF A=0 AND J=9 AND Q1=I AND Q2=8 THEN PRINT : GOTO 4640
4630 IF A=0 THEN PRINT":"
4640 IF A=5 THEN PRINT
4650 PRINTO1$ : NEXTI : A1=0 : GOTO 800
4660 REM STATUS REPORT
4670 PRINT : PRINT "STATUS REPORT:" : X$="" : IFK9>1THENX$="S"
4680 PRINT : PRINT" KLINGONS LEFT :";K9
4690 PRINT" ENERGY        :";INT(E+S)
4700 PRINT" TORPEDOES     :";INT(P)
4710 PRINT : PRINT" MISSION MUST BE COMPLETED IN";.1*INT((T0+T9-T)*10)
4720 PRINT" STARDATES"
4730 X$="S" : IFB9<2THENX$="" : IFB9<1THEN GOTO 4770
4740 PRINT : PRINT" THE FEDERATION IS MAINTAINING";B9
4750 PRINT " STARBASE";X$;" IN THE GALAXY"
4760 GOTO 2950
4770 PRINT : PRINT"YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN"
4780 PRINT"IN THE GALAXY -- YOU HAVE NO STARBASES"
4790 PRINT"LEFT!" : GOTO 2950
4800 REM TORPEDO, BASE NAV, D/D CALCULATOR
4810 IFK3<=0THEN GOTO 2130
4820 X$="" : IFK3>1THENX$="S"
4830 PRINT : PRINT"FROM ENTERPRISE TO KLINGON SHIP";
4840 PRINT X$
4850 H8=0 : FORI=1TO3 : IFK(I,3)<=0THEN GOTO 5320
4860 W1=K(I,1) : X=K(I,2)
4870 C1=S1 : A=S2 : GOTO 5000
4880 PRINT : PRINT"DIRECTION/DISTANCE CALCULATOR:"
4890 PRINT : PRINT"YOU ARE AT QUADRANT ";Q1;",";Q2
4900 PRINT"             SECTOR ";S1;",";S2
4910 PRINT : LX=4 : PRINT"ENTER INITIAL COORDINATES (Y) :  "; : GOSUB 5860
4920 C1=VAL(LII$) : IF C1=0 THEN PRINT : PRINT"CALCULATION ABORTED!" : GOTO 890
4930 LX=4 : PRINT"ENTER INITIAL COORDINATES (X) :  "; : GOSUB 5860
4940 A=VAL(LII$) : IF A=0 THEN PRINT : PRINT"CALCULATION ABORTED!" : GOTO 890
4950 PRINT : LX=4 : PRINT"ENTER FINAL COORDINATES   (Y) :  "; : GOSUB 5860
4960 W1=VAL(LII$) : IF W1=0 THEN PRINT : PRINT"CALCULATION ABORTED!" : GOTO 890
4970 LX=4 : PRINT"ENTER FINAL COORDINATES   (X) :  "; : GOSUB 5860
4980 X=VAL(LII$) : IF X=0 THEN PRINT : PRINT"CALCULATION ABORTED!" : GOTO 890
4990 IF C1=W1 AND A=X THEN PRINT : PRINT"NO RESULTS POSSIBLE!" : GOTO 890
5000 X=X-A : A=C1-W1 : IFX<0THEN GOTO 5140
5010 IFA<0THEN GOTO 5160
5020 IFX>0THEN GOTO 5040
5030 IFA=0THENC1=5 : GOTO 5050
5040 C1=1
5050 IFABS(A)<=ABS(X)THEN GOTO 5100
5060 PRINT : PRINT" DIRECTION =";
5070 D2=C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A))
5080 D2=D2*1000 : D2=D2+0.5 : D2=INT(D2) : D2=D2/1000
5090 PRINT D2 : GOTO 5260
5100 PRINT : PRINT" DIRECTION =";
5110 D2=C1+(ABS(A)/ABS(X))
5120 D2=D2*1000 : D2=D2+0.5 : D2=INT(D2) : D2=D2/1000
5130 PRINT D2 : GOTO 5260
5140 IFA>0THENC1=3 : GOTO 5170
5150 IFX<>0THENC1=5 : GOTO 5050
5160 C1=7
5170 IFABS(A)>=ABS(X)THEN 5220
5180 PRINT : PRINT" DIRECTION =";
5190 D2=C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X))
5200 D2=D2*1000 : D2=D2+0.5 : D2=INT(D2) : D2=D2/1000
5210 PRINT D2 : GOTO 5260
5220 PRINT : PRINT" DIRECTION =";
5230 D2=C1+(ABS(X)/ABS(A))
5240 D2=D2*1000 : D2=D2+0.5 : D2=INT(D2) : D2=D2/1000
5250 PRINT D2
5260 PRINT" DISTANCE  =";
5270 D2=SQR(X^2+A^2)
5280 IF A1=3 OR A1=2 THEN D2=D2/10
5290 D2=D2*1000 : D2=D2+0.5 : D2=INT(D2) : D2=D2/1000
5300 PRINT D2
5310 IFH8=1THEN GOTO 800
5320 NEXTI : GOTO 800
5330 IFB3<>0THEN PRINT : PRINT"FROM ENTERPRISE TO STARBASE" : W1=B4 : X=B5 : GOTO 4870
5340 PRINT : PRINT"MR. SPOCK REPORTS, SENSORS SHOW NO"
5350 PRINT"STARBASES IN THIS QUADRANT." : GOTO 800
5360 REM FIND EMPTY PLACE IN QUADRANT (FOR THINGS)
5370 A$="   "
5375 R1=FNR(1) : R2=FNR(1) : Z1=R1 : Z2=R2 : GOSUB 5560 : IFZ3=0THEN GOTO 5370
5380 RETURN
5390 REM INSERT IN STRING ARRAY FOR QUADRANT
5400 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
5410 IF LEN(A$)<>3THEN PRINT"ERROR" : STOP
5420 IFS8=1THENQ$=A$+RIGHT$(Q$,189) : RETURN
5430 IFS8=190THENQ$=LEFT$(Q$,189)+A$ : RETURN
5440 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8) : RETURN
5450 REM PRINTS DEVICE NAME
5460 ON R1 GOTO 5470,5480,5490,5500,5510,5520,5530,5540
5470 G2$="WARP ENGINES" : RETURN
5480 G2$="SHORT RANGE SENSORS" : RETURN
5490 G2$="LONG RANGE SENSORS" : RETURN
5500 G2$="PHASER CONTROL" : RETURN
5510 G2$="PHOTON TUBES" : RETURN
5520 G2$="DAMAGE CONTROL" : RETURN
5530 G2$="SHIELD CONTROL" : RETURN
5540 G2$="LIBRARY-COMPUTER" : RETURN
5550 REM STRING COMPARISON IN QUADRANT ARRAY
5560 Z1=INT(Z1+.5) : Z2=INT(Z2+.5) : S8=(Z2-1)*3+(Z1-1)*24+1 : Z3=0
5570 IFMID$(Q$,S8,3)<>A$THENRETURN
5580 Z3=1 : RETURN
5590 REM QUADRANT NAME IN G2$ FROM Z4,Z5 (=Q1,Q2)
5600 REM CALL WITH G5=1 TO GET REGION NAME ONLY
5610 IFZ5<=4THEN ON Z4 GOTO5630,5640,5650,5660,5670,5680,5690,5700
5620 GOTO 5710
5630 G2$="ANTARES" : GOTO 5800
5640 G2$="RIGEL" : GOTO 5800
5650 G2$="PROCYON" : GOTO 5800
5660 G2$="VEGA" : GOTO 5800
5670 G2$="CANOPUS" : GOTO 5800
5680 G2$="ALTAIR" : GOTO 5800
5690 G2$="SAGITTARIUS" : GOTO 5800
5700 G2$="POLLUX" : GOTO 5800
5710 ON Z4 GOTO 5720,5730,5740,5750,5760,5770,5780,5790
5720 G2$="SIRIUS" : GOTO 5800
5730 G2$="DENEB" : GOTO 5800
5740 G2$="CAPELLA" : GOTO 5800
5750 G2$="BETELGEUSE" : GOTO 5800
5760 G2$="ALDEBARAN" : GOTO 5800
5770 G2$="REGULUS" : GOTO 5800
5780 G2$="ARCTURUS" : GOTO 5800
5790 G2$="SPICA"
5800 IFG5<>1 THEN ON Z5 GOTO 5820,5830,5840,5850,5820,5830,5840,5850
5810 RETURN
5820 G2$=G2$+" I" : RETURN
5830 G2$=G2$+" II" : RETURN
5840 G2$=G2$+" III" : RETURN
5850 G2$=G2$+" IV" : RETURN
5860 REM ** LX = MAX INPUT  LII$ = OUTPUT STRING **
5870 PRINT BCOL$;CHR$(162);CHR$(157);
5880 LII$="" 
5890 GET Y$ 
5900 IF LEN(LII$)=LX THEN PRINT CHR$(187);CHR$(157); : GOTO 5910
5910 IF Y$="" THEN GOTO 5890
5920 IF ASC(Y$)=20 THEN GOTO 5970
5930 IF ASC(Y$)=13 AND LX=0 THEN PRINT "..."; : PRINT FCOL$ : RETURN
5940 IF ASC(Y$)=13 THEN PRINT " "; : PRINT FCOL$ : RETURN
5950 IF ASC(Y$)=32 THEN GOTO 5990
5960 IF ASC(Y$)<46 OR ASC(Y$)>90 THEN GOTO 5890
5970 IF ASC(Y$)=20 AND LEN(LII$)<1 THEN GOTO 5890
5980 IF Y$=CHR$(20) THEN Y$="" : LII$=LEFT$(LII$,(LEN(LII$)-1)) : PRINT CHR$(20);
5990 IF LEN(LII$)<LX THEN PRINT Y$;CHR$(162);CHR$(157);
6000 IF LEN(LII$)<LX THEN LII$=LII$+Y$
6010 GOTO 5890
6020 REM ** PAUSE WITHOUT CR**
6030 IF CRSTART=1 THEN CR$="PRESS RETURN TO BEGIN "
6040 IF CRSTART=0 THEN CR$="PRESS RETURN TO CONTINUE "
6050 LX=0 : PRINT CR$; : GOSUB 5860
6060 CRSTART=0
6070 RETURN
6080 REM ** KEY TO SRS ICONS **
6090 PRINT : PRINT "KEY TO SHORT RANGE SCANNER ICONS:" : PRINT
6100 PRINT " ";BCOL$;"E";FCOL$;" = THE USS ENTERPRISE"
6110 PRINT " ";DCOL$;"B";FCOL$;" = FEDERATION STARBASE"
6120 PRINT " ";HCOL$;"*";FCOL$;" = STAR"
6130 PRINT " ";ECOL$;"K";FCOL$;" = KLINGON BATTLE CRUISER"
6140 RETURN
6150 REM ** LIST OF COMMANDS **
6160 PRINT : PRINT"USE THESE COMMANDS:" : PRINT
6170 PRINT" NAV - TO SET COURSE"
6180 PRINT" SRS - FOR SHORT RANGE SCAN"
6190 PRINT" LRS - FOR LONG RANGE SCAN"
6195 PRINT" SLS - FOR SHORT & LONG RANGE SCAN"
6200 PRINT" PHA - TO FIRE PHASERS"
6210 PRINT" TOR - TO FIRE PHOTON TORPEDOES"
6220 PRINT" SHE - TO RAISE OR LOWER SHIELDS"
6230 PRINT" DAM - FOR DAMAGE CONTROL REPORTS"
6240 PRINT" COM - TO CALL ON LIBRARY-COMPUTER"
6250 PRINT" KEY - DISPLAY KEY TO SRS ICONS"
6260 PRINT" HLP - THIS LIST OF COMMANDS"
6270 PRINT" XXX - TO RESIGN YOUR COMMAND"
6280 RETURN
6290 REM ** DIRECTION HELPER **
6300 PRINT
6310 PRINT " ENTER A NUMERIC      4  3  2"
6320 PRINT " BETWEEN 1 AND 9       . . ."
6330 PRINT "                        ..."
6340 PRINT " DECIMALS MAY BE    5 ---*--- 1"
6350 PRINT " USED                   ..."
6360 PRINT " (IE 8.527 ETC.)       . . ."
6370 PRINT "                      6  7  8"
6380 RETURN
6390 REM ** GALAXY MAP ROMAN NUMERALS **
6400 PRINT CHR$(146); : IF J=Q2 THEN PRINT CHR$(18);
6410 IF J=1 OR J=5 THEN PRINT " I ";;
6420 IF J=2 OR J=6 THEN PRINT "II ";
6430 IF J=3 OR J=7 THEN PRINT "III";
6440 IF J=4 OR J=8 THEN PRINT "IV ";
6450 PRINT CHR$(146);" "; : RETURN
6500 REM ** COLOURS FOR IN-GAME **
6510 ACOL$=CHR$(144) : REM 144 = (00) BLACK
6520 BCOL$=CHR$(005) : REM 005 = (01) WHITE
6530 CCOL$=CHR$(028) : REM 028 = (02) RED
6540 DCOL$=CHR$(159) : REM 159 = (03) CYAN
6550 ECOL$=CHR$(156) : REM 156 = (04) VIOLET
6560 FCOL$=CHR$(030) : REM 030 = (05) GREEN
6570 GCOL$=CHR$(031) : REM 031 = (06) BLUE
6580 HCOL$=CHR$(158) : REM 158 = (07) YELLOW
6590 ICOL$=CHR$(129) : REM 129 = (08) ORANGE
6650 REM ** COLOURS FOR INITIALISATION **
6690 POKE 53280,0 : POKE 53281,0 : POKE 646,5 : RETURN
6695 RETURN